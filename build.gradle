plugins {
    id 'java'
    id 'com.gorylenko.gradle-git-properties' version '2.5.4'
}

group = 'org.tmvn.stock_ingestor'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

repositories {
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/tamm0ngv0ngan/packages")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    mavenCentral()
}

tasks.register('checkGitStatus') {
    doLast {
        def status = 'git diff --quiet'.execute()
        status.waitFor()
        if (status.exitValue() != 0) {
            throw new GradleException("Uncommitted changes detected! Please commit or stash changes before building")
        }
    }
}

tasks.named('build') {
    dependsOn checkGitStatus
}

tasks.named('jar') {
    dependsOn checkGitStatus
}

dependencies {
    implementation 'org.tmvn.lib:git-info:1.0.0'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
}

test {
    useJUnitPlatform()
}